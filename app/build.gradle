apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'realm-android'

android {
    signingConfigs {
        release {
            def properties = new Properties()
            properties.load(project.rootProject.file('local.properties').newDataInputStream())
            def key = properties.getProperty('key')
            def password = properties.getProperty('password')

            keyAlias key
            keyPassword password
            storeFile file('keystore/ms.jks')
            storePassword password
        }
    }

    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId "com.juniperphoton.myersplash"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 306
        versionName "3.0.4"

        def properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        def unsplashKey = properties.getProperty('unsplashKey')

        buildConfigField 'String', "UNSPLASH_APP_KEY", "\"" + unsplashKey + "\""
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            resValue "string", "authorities", "com.juniperphoton.myersplash.debug.fileProvider"
            manifestPlaceholders = [
                    "APP_NAME": "MyerSplash DEBUG"
            ]
        }
        release {
            resValue "string", "authorities", "com.juniperphoton.myersplash.fileProvider"
            manifestPlaceholders = [
                    "APP_NAME": "MyerSplash"
            ]
            signingConfig signingConfigs.release

            android.applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "myersplash_release_build.${defaultConfig.versionName}" +
                            ".${defaultConfig.versionCode}.apk"
                }
            }
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        main.res.srcDirs += 'src/debug'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_coroutine_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlin_coroutine_version"

    testImplementation 'junit:junit:4.12'

    implementation "androidx.legacy:legacy-support-v13:$androidx_version"
    implementation "androidx.appcompat:appcompat:$androidx_app_compat_version"
    implementation "com.google.android.material:material:$androidx_version"
    implementation "androidx.cardview:cardview:$androidx_version"
    implementation "androidx.browser:browser:$androidx_version"
    implementation "androidx.palette:palette:$androidx_version"
    implementation 'androidx.constraintlayout:constraintlayout:1.1.2'

    implementation "com.jakewharton:butterknife:${butterknife_version}"
    kapt "com.jakewharton:butterknife-compiler:${butterknife_version}"

    implementation 'com.facebook.fresco:fresco:1.5.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.5'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation 'com.google.code.gson:gson:2.8.0'
    implementation 'org.greenrobot:eventbus:3.0.0'
    implementation 'com.google.android:flexbox:1.1.0'
    implementation 'com.juniperphoton:flipperlayout:1.2.1'
    kapt 'io.realm:realm-annotations:3.0.0'
    kapt 'io.realm:realm-annotations-processor:3.0.0'
    //dagger
    implementation 'com.google.dagger:dagger:2.11'
    kapt 'com.google.dagger:dagger-compiler:2.11'
}
